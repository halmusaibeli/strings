PROGRAM test_strings
%NOLOCKGROUP

%include strings.klt
%include strings.klh
%include kunit.klh

ROUTINE t_split_str : BOOLEAN
  VAR
    s : STRING[16]
    o : ARRAY[4] OF STRING[16]
  BEGIN
    s = 'one,two,three'
    split_str(s,',',o)

    RETURN(kunit_eq_str('one',o[1]) AND
      kunit_eq_str('two',o[2]) AND
      kunit_eq_str('three',o[3]) AND
      kunit_eq_str('',o[4]))
  END t_split_str

ROUTINE t_extrat_str : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('one', extract_str('one,two,three', ',', 1)) AND
           kunit_eq_str('two', extract_str('one,two,three', ',', 2)) AND
           kunit_eq_str('three', extract_str('one,two,three', ',', 3))  )
  END t_extrat_str

ROUTINE t_rev_split : BOOLEAN
  VAR
    s : STRING[30]
    o : ARRAY[5] OF STRING[10]
  BEGIN
    s = 'C:\user\name\folder\file.ext'
    rev_split(s,'\',o)

    RETURN(kunit_eq_str('file.ext',o[1]) AND
      kunit_eq_str('folder',o[2]) AND
      kunit_eq_str('name',o[3]) AND
      kunit_eq_str('user',o[4]) AND
      kunit_eq_str('C:',o[5]))
  END t_rev_split

ROUTINE t_fnam_parse : BOOLEAN
  VAR
    s1 : STRING[30]
    s2 : STRING[12]
  BEGIN
    s1 = 'FR:\user\name\folder\file.ext'
    s2 = 'MC:kl6004.xml'

    RETURN(kunit_eq_str('ext',get_ext(s1)) AND
           kunit_eq_str('file',basename(splitext(s1)) ) AND
           kunit_eq_str('kl6004',basename(splitext(s2)) ) AND
           kunit_eq_str('FR',get_device(s1)) AND kunit_eq_str('MC',get_device(s2)) )
  END t_fnam_parse

ROUTINE t_progvar : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('prog',get_progname('[prog]var')) AND 
           kunit_eq_str('var',get_varname('[prog]var')) AND 
           kunit_eq_str('',get_progname('var[1]')) AND 
           kunit_eq_str('var[1]',get_varname('var[1]')) AND
           kunit_eq_str('var[1]',get_varname('[prog]var[1]')) )
  END t_progvar

ROUTINE t_char_cnt : BOOLEAN
  BEGIN
    RETURN(kunit_eq_int(4, char_count('one,two,three,four,', ',')) AND
          kunit_eq_int(4,char_count('oneoneonetwoone', 'one')) AND
          kunit_eq_int(13,char_count('....F......F...', '.')) AND
          kunit_eq_int(2,char_count('....F......F...', 'F')) AND
          kunit_eq_int(0,char_count('....F......F...', '*')))
  END t_char_cnt

ROUTINE t_remov : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str(',two,three', remove_char('one,two,three', 'one')) AND
           kunit_eq_str('one,,three', remove_char('one,two,three', 'two')) AND
           kunit_eq_str('one,two,', remove_char('one,two,three', 'three')) AND 
           kunit_eq_str(',,', remove_char('one,one,one', 'one')) AND
           kunit_eq_str('FUT-1,0,1', remove_char('F U T -1, 0, 1', ' ')) )
  END t_remov

ROUTINE t_lstrip : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('test', lstrip('    test')) AND
      kunit_eq_str('test', lstrip('test')))
  END t_lstrip

ROUTINE t_rstrip : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('test', rstrip('test    ')) AND
      kunit_eq_str('  test', rstrip('  test  ')) AND
      kunit_eq_str('  test', rstrip('  test')))
  END t_rstrip

ROUTINE t_b_to_s : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('true', b_to_s(true)) AND
      kunit_eq_str('false', b_to_s(false)))
  END t_b_to_s

ROUTINE t_i_to_s : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('42', i_to_s(42)) AND
      kunit_eq_str('-42', i_to_s(-42)))
  END t_i_to_s

ROUTINE t_r_to_s : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('3.14', r_to_s(3.14)) AND
      kunit_eq_str('1.234', r_to_s(1.234)) AND
      kunit_eq_str('-3.14', r_to_s(-3.14)) AND
      kunit_eq_str('0.5', r_to_s(0.5)) AND
      kunit_eq_str('10.0', r_to_s(10.000)))
  END t_r_to_s

ROUTINE t_p_to_s : BOOLEAN
  VAR
    p : XYZWPR
  BEGIN
    p.x = 1.1
    p.y = 2.2
    p.z = 3.3
    p.w = 4.4
    p.p = 5.5
    p.r = 6.6

   RETURN(kunit_eq_str('X: 1.1 Y: 2.2 Z: 3.3' + chr(13) &
     + 'W: 4.4 P: 5.5 R: 6.6', p_to_s(p)))
  END t_p_to_s

ROUTINE t_delim_pos : BOOLEAN
  VAR
    p : XYZWPR
  BEGIN
    p.x = 1.1 ; p.y = 2.2 ; p.z = 3.3 ;
    p.w = 4.4 ; p.p = 5.5 ; p.r = 6.6 ;

    RETURN(kunit_eq_str('1.1,2.2,3.3,4.4,5.5,6.6', pose_to_s(p, ',')))
  END t_delim_pos

ROUTINE t_vec2str : BOOLEAN
  VAR
    v : VECTOR
  BEGIN
    v.x = 20 ; v.y = 80.5 ; v.z = 150.899 ;

    RETURN(kunit_eq_str('20.0,80.5,150.899', vec_to_s(v, ',')))
  END t_vec2str

ROUTINE t_joint2str : BOOLEAN
  VAR
    pose : JOINTPOS6
    arr : ARRAY[6] OF REAL
    status : INTEGER
    jointstr : STRING[50]
  BEGIN
    -- set jointpos type
    arr[1] = 10.0
    arr[2] = 30.0
    arr[3] = 190.0
    arr[4] = -90.0
    arr[5] = 0.0
    arr[6] = 180.0

    CNV_REL_JPOS(arr, pose, status)
    IF status <> 0 THEN
      WRITE TPDISPLAY(CR, 'problem in t_joint2str converting array.')
      RETURN(FALSE)
    ENDIF

    jointstr = joint_to_s((pose), ',')
    RETURN(kunit_eq_str('10.0,30.0,190.0,-90.0,0.0,180.0', jointstr))

  END t_joint2str

ROUTINE t_str2joint : BOOLEAN
  VAR
    jointstr : STRING[50]
    pose : JOINTPOS6
    arr : ARRAY[6] OF REAL
    status : INTEGER
  BEGIN
    jointstr = '10.0,30.0,190.0,-90.0,0.0,180.0'
    pose = s_to_joint(jointstr, ',')
    --convert to real array for validation
    CNV_JPOS_REL(pose, arr, status)
    IF (status <> 0) THEN
      WRITE TPDISPLAY(CR, 'problem in t_str2joint converting jointpose.')
      RETURN(FALSE)
    ENDIF

    RETURN(kunit_eq_r(10.0, arr[1]) AND kunit_eq_r(190.0, arr[3]) AND
          kunit_eq_r(-90.0, arr[4]) AND kunit_eq_r(0.0, arr[5]))
  END t_str2joint

ROUTINE t_pose_conf : BOOLEAN
  BEGIN
  END t_pose_conf

ROUTINE t_esc_quotes : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('\"test\"', esc_quotes('"test"')))
  END t_esc_quotes

ROUTINE t_splitStr
VAR
	o : ARRAY[4] OF STRING[16]
BEGIN
	split_str('Server: KHTTPD/0.1', ':', o)
	kunit_test('first one', kunit_eq_str('Server', o[1]))
	kunit_test('second one', kunit_eq_str(' KHTTPD/0.1', o[2]))

	split_str('fooBARbaz', 'BAR', o)
	kunit_test('first=foo', kunit_eq_str('foo', o[1]))
	kunit_test('second=baz', kunit_eq_str('baz', o[2]))
END t_splitStr

ROUTINE t_search_str : BOOLEAN
  VAR
    in_str : STRING[40]
  BEGIN
    in_str = 'The quick brown fox jumps over the lazy dog.'
    RETURN(search_str('quick brown', in_str) AND search_str('jumps', in_str) &
           AND (NOT search_str('grumpy cat', in_str)))
  END t_search_str

ROUTINE t_num_char : BOOLEAN
  BEGIN
    RETURN(charisnum('1') AND charisnum('9') AND charisnum('3') AND NOT charisnum('b'))
  END t_num_char

ROUTINE t_str2int : BOOLEAN
  BEGIN
    RETURN(kunit_eq_int(-3, s_to_i('-3')) AND kunit_eq_int(150, s_to_i('150')) &
           AND kunit_eq_int(876524, s_to_i('876524')) AND kunit_eq_int(3, s_to_i('3.14')))
  END t_str2int

ROUTINE t_str2real : BOOLEAN
  BEGIN
    RETURN(kunit_eq_r(-3.14, s_to_r('-3.14')) AND kunit_eq_r(3.14, s_to_r('3.14')))
  END t_str2real

ROUTINE t_str2rarr : BOOLEAN
  VAR
    rstr : STRING[90]
    r_arr : ARRAY[18] OF REAL
    b : BOOLEAN
  BEGIN
    rstr = '3.21,1.09,5.5,6.00,1.111,8,2.718,3.14159,6.02,111.123'
    rstr = rstr + ',-6.38,1E5,6.626E1,1.618,2,3,9.81,1.0'

    s_to_rarr(rstr, ',', r_arr)

    RETURN(kunit_eq_r(3.21, r_arr[1]) AND kunit_eq_r(5.5, r_arr[3]) AND
          kunit_eq_r(3.14159, r_arr[8]) AND kunit_eq_r(-6.38, r_arr[11]) AND
          kunit_eq_r(6.626E1, r_arr[13]) AND kunit_eq_r(1.0, r_arr[18]))
  END t_str2rarr

ROUTINE t_strreg : BOOLEAN
  VAR
    numreg : INTEGER
    str : STRING[30]
    status : INTEGER
  BEGIN
    -- set string register
    numreg = 6
    str = 'string register test!'
    SET_STR_REG(numreg, str, status)

    RETURN(kunit_eq_str(str, strreg_to_s(numreg)))
  END t_strreg

-- **** TESTS BELOW MANIPULATE REGISTERS!!!
ROUTINE t_numreg2s : BOOLEAN
  VAR
    reg_no : INTEGER
    value : INTEGER
    v_str : STRING[5]
    status : INTEGER
  BEGIN
    reg_no = 10
    value = 2048
    --convert value to string
    v_str = i_to_s(value)
    -- set register
    SET_INT_REG(reg_no, value, status)
    --retrieve numreg as string
    -- and compare
    RETURN(kunit_eq_str(i_to_s(value), numreg_to_s(reg_no)))
  END t_numreg2s

ROUTINE t_posreg2s : BOOLEAN
  VAR
    reg_no : INTEGER
    expected : XYZWPREXT
    uframe : INTEGER
    utool : INTEGER
    STATUS :INTEGER
    conf_str : STRING[25]
  BEGIN
    --reg no
    reg_no = 80
    --store frame info
    uframe = $MNUFRAMENUM[1]
    utool = $MNUTOOLNUM[1]
    --set position
    expected.x = 10.0
    expected.y = 30.0
    expected.z = 190.0
    expected.w = -90.0
    expected.p = 0.0
    expected.r = 180.0
    conf_str = 'F U T, 0, 0, 0'
    --convert to config type
    CNV_STR_CONF(conf_str, expected.config_data, STATUS)
    IF (status <> 0) THEN
      WRITE TPDISPLAY(CR, 'problem in t_posreg2s with config str.')
      RETURN(FALSE)
    ENDIF
    -- set pos reg
    SET_POS_REG(reg_no, (expected), STATUS, 1)
    IF (status <> 0) THEN
      WRITE TPDISPLAY(CR, 'problem in t_posreg2s setting posreg')
      RETURN(FALSE)
    ENDIF
    --compare expected to retrieved
    RETURN(kunit_eq_str(p_to_s_conf(expected, 1, uframe, utool), &
           posreg_to_s(reg_no, 1)))
  END t_posreg2s

ROUTINE t_isreal : BOOLEAN
  BEGIN
    RETURN(kunit_assert(strisreal('5.025E-05')) AND kunit_assert(strisreal('-0.1234')) AND &
           kunit_assert(strisreal('-.12E+02')) AND kunit_assert(strisreal('1988.10')) AND & 
           kunit_assert(strisreal('99999')) AND kunit_assert( NOT strisreal('-01E2.34')) AND &
           kunit_assert(NOT strisreal('FIVE')) AND kunit_assert(NOT strisreal('-E1234')) AND &
           kunit_assert(NOT strisreal('.-9876')) )
  END t_isreal


ROUTINE t_isint : BOOLEAN
  BEGIN
    RETURN(kunit_assert(strisint('-2578')) AND kunit_assert(strisint('0001')) AND & 
           kunit_assert(NOT strisint('3.14159')) AND kunit_assert(NOT strisint('-200.99')) AND &
           kunit_assert(strisint('2')) )
  END t_isint

ROUTINE t_strparse : BOOLEAN
  VAR
    lnth : INTEGER
    str : STRING[120]
    out_str : STRING[120]
  BEGIN
    lnth = 30
    str = 'This message is too long to fit on one line.'+ chr(13) + &
          ' This string should be automatically split apart'+ & 
          ' to fit on the display.'
    --expected
    out_str= 'This message is too long to fi'+ chr(13) +'t on one line.'+ chr(13) + &
          ' This string should be automat'+ chr(13) +'ically split apart to fit on t'+ chr(13) + & 
          'he display.'
    
    RETURN(kunit_eq_str(str_parse(str,lnth), out_str))
  END t_strparse


ROUTINE t_binary : BOOLEAN
  BEGIN
    RETURN(kunit_eq_int(2, bin_to_i('10')) AND kunit_eq_int(255, bin_to_i('11111111')) &
           AND kunit_eq_int(53, bin_to_i('110101')) AND kunit_eq_int(21, bin_to_i('10101')) &
           AND kunit_eq_int(1, bin_to_i('1')) AND kunit_eq_int(0, bin_to_i('0')))
  END t_binary


ROUTINE t_dec2bin : BOOLEAN
  BEGIN
    RETURN(kunit_eq_str('10', i_to_byte(2, FALSE)) AND kunit_eq_str('11111111', i_to_byte(255, FALSE)) &
           AND kunit_eq_str('110101', i_to_byte(53, FALSE)) AND kunit_eq_str('10101', i_to_byte(21, FALSE)) & 
           AND kunit_eq_str('101011', i_to_byte(53, TRUE)) AND kunit_eq_str('1011', i_to_byte(13, TRUE)) )
  END t_dec2bin


BEGIN
  --total asserts 100
  kunit_test('split string', t_split_str) --asserts 4
  kunit_test('extract string', t_extrat_str) --asserts 4
  t_splitStr
  kunit_test('reverse split string', t_rev_split) --asserts 5
  kunit_test('file path parsing', t_fnam_parse) -- assert 5
  kunit_test('char_count()', t_char_cnt) --asserts 5
  kunit_test('remove char from string', t_remov) -- asserts 4
  kunit_test('parsing [prog]var', t_progvar) -- asserts 5
  kunit_test('lstrip()', t_lstrip) --asserts 2
  kunit_test('rstrip()', t_rstrip) --asserts 3
  kunit_test('b_to_s()', t_b_to_s) --asserts 2
  kunit_test('i_to_s()', t_i_to_s) --asserts 2
  kunit_test('r_to_s()', t_r_to_s) --asserts 5
  kunit_test('p_to_s()', t_p_to_s) --asserts 2
  kunit_test('pose2delim_str()', t_delim_pos) --asserts 1
  kunit_test('v_to_s()', t_vec2str) --asserts 1
  kunit_test('joint2str()',t_joint2str) --asserts 1
  kunit_test('str2joint()',t_str2joint) --asserts 4
  kunit_test('esc_quotes()', t_esc_quotes) --asserts 1
  kunit_test('search_str()',t_search_str) --asserts 3
  kunit_test('number_char()',t_num_char) --asserts 4
  kunit_test('str2int()',t_str2int) --asserts 4
  kunit_test('str2real()',t_str2real) --asserts 2
  kunit_test('real_array_from_str()',t_str2rarr) --asserts 6
  kunit_test('numreg_to_str', t_numreg2s) --asserts 1
  kunit_test('posreg_to_str', t_posreg2s) --asserts 1
  kunit_test('string_register_retrieve', t_strreg) --asserts 1
  kunit_test('test if strings are reals', t_isreal) --asserts 9
  kunit_test('test if strings are ints', t_isint) --asserts 5
  kunit_test('str_parse()', t_strparse) --asserts 1
  kunit_test('binary string test', t_binary) --asserts 6
  kunit_test('decimal to binary', t_dec2bin) --asserts 6

  kunit_done
END test_strings
